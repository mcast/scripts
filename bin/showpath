#! /usr/bin/perl -T

use strict;
use warnings ;

use Cwd 'abs_path';
use constant DEFAULT_PATHS => qw( PATH PERL5LIB LD_LIBRARY_PATH PERL_LOCAL_LIB_ROOT );

=head1 NAME

showpath - display path variables in a human-friendly way

=head1 SYNOPSIS

  showpath		# shows some default paths
  showpath MANPATH	# look it up in the environment
  showpath $SOMEPATH	# show path after expansion by the shell

  # show which elements contain ls, cp
  showpath PATH PERL5LIB -- ls cp Test::More
  # (doesn't search @INC elements added by Perl)

=head1 AUTHOR

 Copyright (c) 2009, 2016 Genome Research Ltd.
 Author: Matthew Astley <mca@sanger.ac.uk>

This file is part of "mca-wtsi/scripts".

"mca-wtsi/scripts" is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License as
published by the Free Software Foundation; either version 2 of the
License, or (at your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program. If not, see <http://www.gnu.org/licenses/>.

=cut


sub main {
    showpath(@ARGV);
}

sub showpath {
    my @items = @_;
    unshift @items, DEFAULT_PATHS if !@items || $items[0] eq '--';

    my @find;
    if (my ($idx) = grep { $items[$_] eq '--' } (0 .. $#items)) {
      (undef, @find) = splice @items, $idx;
    }
    push @find, map {
      my $f = $_;
      if ($f =~ s{::}{/}g) {
	("$f.pm");
      } else {
	();
      }
    } @find;

    for (my $i=0; $i < @items; $i++) {
	my $var = $items[$i];
	my ($key, $val);

	# figure out what we're looking at
	if ($var =~ /^\w+$/ && defined $ENV{$var}) {
	    # environment variable
	    $key = $var;
	    $val = $ENV{$key};
	} elsif ($var =~ /^[A-Z]+[A-Z0-9_]+$/) {
	    # looks like an absent environment variable
	    printf("%-20s not set\n\n", $var.":");
	    next;
	} else {
	    # take it literally
	    $key = "#$i";
	    $val = $var;
	}

	# display
	my %seen;
	print "$key:\n";
	foreach my $part (split /:/, $val) {
	    $part =~ s{/+$}{};

	    my @flag;
	    push @flag, 'dir' if -d $part;
	    push @flag, 'ABSENT' if !-e _;
	    push @flag, 'DUP' if $seen{$part} ++;
	    push @flag, 'REL' if $part !~ m{^/}; # XXX: UN*Xism

	    my $symlink = '';
	    if (-l $part) {
		push @flag, 'symlink';
		my $ln = readlink($part);
		$symlink = " -> $ln";
		my $real = abs_path($part);
		$symlink .= " -> $real" unless $real eq $ln;
		push @flag, "ln2DUP" if $seen{$real};
	    }

	    printf("  %-60s %s%s\n", $part, (join ",", @flag), $symlink);

	    # show which of @find we see in here
	    foreach my $find (@find) {
	      my $fn = "$part/$find";
	      next unless -e $fn;
	      my $type = -f _ ? 'file' : (-d _ ? 'dir' : 'other');
	      $type = "symlink to $type" if -l $fn; # TODO: share readlink stuff above
	      printf("      %-60s %s\n", $find, $type);
	    }
	}
	print "\n";
    }
}

main();
